plugins {
    id 'java' // java plugin must be explicitly applied for shadow plugin
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.poi:poi-ooxml:4.1.2'
    implementation 'com.fasterxml:aalto-xml:1.3.0'
    implementation 'stax:stax-api:1.0.1'
}

shadowJar {
    exclude 'repackage/**'

    exclude 'LICENSE.txt'
    exclude 'NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/maven/**'

    // these are only needed when handling Visio files, remove it if you would like to use Visio support
    dependencies {
        exclude(dependency('com.github.virtuald:curvesapi'))
        exclude(dependency('commons-codec:commons-codec'))
        exclude(dependency('commons-codec:commons-collections4'))
    }

    // Relocate javax dependencies so Android does not choke
    relocate 'javax.xml.namespace', 'org.apache.poi.javax.xml.namespace'
    relocate 'javax.xml.stream', 'org.apache.poi.javax.xml.stream'
    relocate 'javax.xml.XMLConstants', 'org.apache.poi.javax.xml.XMLConstants'

    // java.awt is not available on Android, but java.awt.Color is used in APIs in POI,
    // therefore relocate this class to another one where we can include a rewrite
    relocate 'java.awt.Color', 'org.apache.poi.java.awt.Color'
    relocate 'java.awt.font.FontRenderContext', 'org.apache.poi.java.awt.font.FontRenderContext'
    relocate 'java.awt.font.TextLayout', 'org.apache.poi.java.awt.font.TextLayout'
    relocate 'java.awt.geom.Rectangle2D', 'org.apache.poi.java.awt.geom.Rectangle2D'
}

jar.dependsOn shadowJar
test.dependsOn shadowJar
