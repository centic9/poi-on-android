apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // commons-text 1.8 fails due to Java 8 dependency
    implementation 'org.apache.commons:commons-text:1.9'
    // for unit-tests copied from Apache POI source, 1.64 did not work!
    //implementation 'org.bouncycastle:bcpkix-jdk15on:1.59'

    // do not change this to "test",
    // there is a unit-tests executed in normal code currently...
    implementation 'junit:junit:4.13.2'

    //testImplementation "com.android.support.test:runner:1.0.2"

    // newer versions of xmlsec are compiled with JDK 8 which does not work in Android Studio 2.x!
    /*implementation ('org.apache.santuario:xmlsec:2.2.1') {
        // break compilation because it includes stax-api which is under forbidden package 'javax.xml.stream'
        exclude group: 'org.codehaus.woodstox'
    }*/

    implementation files('../poishadow/build/libs/poishadow-all.jar')

    // Testing-only dependencies
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc02'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    //androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'
}

preBuild.dependsOn ':poishadow:shadowJar'

android {
    compileSdkVersion 31
    buildToolsVersion '29.0.3'

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'org.dstadler.poiandroidtest.poitest'
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 3
        versionName '4.1.2'

        // Enabling multidex support.
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // For the purposes of the sample, allow testing of a proguarded release build
            // using the debug key
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        // There are restricted packages used in some of POI code, e.g. java.awt or javax.imageio
        // I could not exclude these in the
        abortOnError false
    }

    // error about duplicates if some of these are not excluded
    packagingOptions {
        pickFirst 'META-INF/DEPENDENCIES.txt'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/notice.txt'
        pickFirst 'META-INF/license.txt'
        pickFirst 'META-INF/dependencies.txt'
        pickFirst 'META-INF/LGPL2.1'
    }
}
