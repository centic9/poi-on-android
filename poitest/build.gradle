apply plugin: 'com.android.application'

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // commons-text 1.8 fails due to Java 8 dependency
    implementation 'org.apache.commons:commons-text:1.13.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'

    // there is a unit-tests executed in normal code currently...
    implementation 'org.junit.jupiter:junit-jupiter-api:5.12.1'

    // newer versions of xmlsec are compiled with JDK 8 which does not work in Android Studio 2.x!
    /*implementation ('org.apache.santuario:xmlsec:2.2.1') {
        // break compilation because it includes stax-api which is under forbidden package 'javax.xml.stream'
        exclude group: 'org.codehaus.woodstox'
    }*/

    implementation files('../poishadow/build/libs/poishadow-all.jar')

    // Testing-only dependencies
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc02'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    //androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
}

preBuild.dependsOn ':poishadow:shadowJar'

android {
    compileSdkVersion 34

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'org.dstadler.poiandroidtest.poitest'
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 6
        versionName '5.3.0'

        // Enabling multidex support.
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // For the purposes of the sample, allow testing of a proguarded release build
            // using the debug key
            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        resources {
            pickFirsts += [
                    'META-INF/DEPENDENCIES.txt',
                    'META-INF/LICENSE.txt',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md',
                    'META-INF/NOTICE.txt',
                    'META-INF/NOTICE',
                    'META-INF/LICENSE',
                    'META-INF/DEPENDENCIES',
                    'META-INF/notice.txt',
                    'META-INF/license.txt',
                    'META-INF/dependencies.txt',
                    'META-INF/LGPL2.1'
            ]
        }
    }

    // error about duplicates if some of these are not excluded
    lint {
        abortOnError false
    }
    namespace 'org.dstadler.poiandroidtest.poitest'
    buildFeatures {
        buildConfig true
    }
}
